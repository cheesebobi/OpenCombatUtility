plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.3-SNAPSHOT" apply false
}

//Load Access Properties (ghReadPAT, ghUsr)
ext.accessProps = new Properties()
file("access.properties").withInputStream {
    accessProps.load(it)
}


architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    base {
        archivesName = "$rootProject.archives_base_name-${project.loader}-$minecraft_version"
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the yarn mappings you may select this one as well.
        mappings "net.fabricmc:yarn:1.19.4+build.2:v2"

        modImplementation "com.LubieKakao1212.qulib:qulib-kt-${project.loader}-${minecraft_version}:${qulib_version}"

        modCompileOnly "cc.tweaked:cc-tweaked-${minecraft_version}-core-api:${cctVersion}"
        modCompileOnly "cc.tweaked:cc-tweaked-${minecraft_version}-${project.loader}-api:${cctVersion}"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven {
            name = "GitHubPackages QuLib"
            url = uri("https://maven.pkg.github.com/LubieKakao1212/qulib-kt")
            credentials {
                username = accessProps.ghUsr
                password = accessProps.ghReadPAT
            }
        }
        maven {
            url "https://squiddev.cc/maven/"
            content {
                includeGroup("cc.tweaked")
                includeModule("org.squiddev", "Cobalt")
            }
        }
        maven { url 'https://maven.wispforest.io' }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
